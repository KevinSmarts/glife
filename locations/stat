# stat
!translation to Chinese, by illume
clr
view
rand1=rand(0,100)
$stat_msg=''
if poSkill > 1000:poSkill = 1000

if cheatHealth = 1:dynamic $din_std_cure

if analplay >= 2 and anus <= 10:anus += 1 & analplay = 0
if analplay >= 4 and anus <= 15:anus += 1 & analplay = 0
if analplay >= 8 and anus <= 20:anus += 1 & analplay = 0

musle[1] = musle
flex[1] = agil
stamina[1] = vital * 3
wipo[1] = will
intel[1] = intel - bimbostupidity
krasota[1] = vnesh
rost[1] = rost
salo[1] = salo
fat[1] = fat
Bteh[1] = bteh
Uteh[1] = uteh
Zteh[1] = zteh
!!flex[0] = agil
!!stamina[0] = vital * 3
!!krasota[0] = vnesh
!!wipo[0] = will

!!!CHEATS!!!
if cheatWork = 1:work = 0
if cheatWorkKiskis = 1:young_shop_miss = 0
if cheatHapri = 1:hapri = 1
if cheatKlisma = 1:klismaday = daystart & klismaday1 = 1

if cheatNomakeup = 1:
	if mop = 0:mop = 1
end

if cheatNosweat = 1 and sweat > 0:sweat = 0

if cheatNoEat = 1:
	if energy < 35:energy = 35
	if water < 35:water = 35
end

if cheatpirate = 1 and pirathorny < 20:pirathorny = 20

if CheatNoSleep = 1 and son < 35:son = 35

if alko > 0 and frost > 0:frost = 0
if StrongNarkota >= 20:
    if alko > 0:alko = 0
end
if lernSkill >= 8:intel += 1 & lernSkill = 0

if lern >= 10:intel += 1 & lern = 0
if GorSlut = 1:
	$gnikname = '賣弄風騷'
elseif GorSlut = 2:
	$gnikname = '口交女'
elseif GorSlut = 3:
	$gnikname = '經常口交'
elseif GorSlut = 4:
	$gnikname = '容易推倒'
elseif GorSlut = 5:
	$gnikname = '性愛上癮'
elseif GorSlut = 6:
	$gnikname = '妓女'
end

if GorSlut >= 2:KatjaKnowBJ = 1
if GorSlut >= 1 or preg > 0:KatjaKnowSex = 1
if GorSlut > 6: GorSlut = 6

if film > 0:
 !pornoaktrissa
	$kompromatFoto = 'The photo is a screenshot from one of your porn movies.'

elseif fotoyousuck = 1:
	!!sucked in a couple of guys and was sfotografirovanna
	$kompromatFoto = 'The picture shows you sucking two cocks at the same time.'

elseif fotoCFNMsex > 0:
	$kompromatFoto = 'In the photo you see yourself getting fucked from behind by a random guy. This must have been taken in the stripclub.'

elseif fotoCFNMblowjobCum > 0:
	!!cum on face in the strip club
	$kompromatFoto = 'In the photo you are sitting infront of a guy, kissing his cock. There seems to be cum on your face. This photo must have been taken in the stripclub.'

elseif fotoCFNMblowjob > 0:
	!!sucked in front of everyone
	$kompromatFoto = 'In the photo you are giving a guy a blowjob. You can spot a group of women in the background and it seems like this photo was taken in the stripclub.'

elseif stripdancesum > 0:
	!!stripper
	$kompromatFoto = 'In the photo you are dancing on a pole. Obviously someone photographed you, while you were on stage at the stripclub.'

elseif modelfoto > 0:
	!!photo model
	$kompromatFoto = 'The photo shows you naked, it is very well taken and must be from a shoot at the modeling agency.'
end

shameless = guy + bj + anal + hj + ((slutty + gang) * 2)
moneySUM = money + karta + stolmoney
cumSUM = cumsumbod/40

if husbizvradd > 5:izvratH = 1
if husbharmin > 10:harakHusb = 0
if clrbelo < 0:clrbelo = 0 & tanga = 0

if husband = 0:
	husbizvradd = 0
	husbharmin = 0
	husbandMark = 0
	houseslut = 0
end

if SNarkvnesh = 0:SNarkvnesh = 1

if SNarkTimes >= SNarkvnesh * 25:
	SNarkvnesh += 1
	vidage += 1
end

if gopota >= 20 and gnewQW = 1:gnewQW = 2
if workDisk = 4 and week > 5:workDisk = 1
if workDisk = 3 and week > 5:workDiskAtkPlayer = 1

if fat < 0:fat = 0
if vokal < 0:vokal = 0
if vokal > 100:vokal = 100
if dance > 100:dance = 100
if stripdance > 100:stripdance = 100
if poledance > 100:poledance = 100


if bfa < 0:bfa = 0
if painQW = 0:painweather = 6
if beg < 0:beg = 0
if beg > 2000:beg = 2000
if oficiant > 100:oficiant = 100
if vagina > 36:vagina = 36
if throat > 36:throat = 36
if anus > 36:anus = 36
if Jab > 120:Jab = 120
if Punch > 120:Punch = 120
if Kik > 120:Kik = 120
if KikDef > 120:KikDef = 120
if NarkImmune = 1:
	if narkoman ! 0:narkoman = 0
elseif nark >= 30 and narkoman = 0 and NarkImmune = 0:
	narkoman = 1
elseif nark = 0 and narkoman = 1:
	narkoman = 0
end

if CheatSlut = 1:
	mesec = 0
	FocH = 0
	EggRH = 0
	Ovulate = 0
	fertegg = 0
	UnfertEgg = 0
	PregChem = 0
	preg = 0
	LutH = 0
	RecovH = 0
	babyembryo = 0
	cycle = 1
	if horny < 50: horny = 50
end
if fairycurse >= daystart: horny = 90
boxing = Jab + Punch + Kik + KikDef

if boxing > 100:boxing = 100

if stren > 100 and mutagenP = 0:stren = 100
if speed > 100 and mutagenS = 0:speed = 100
if agil > 100 and mutagenA = 0:agil = 100
if react > 100 and mutagenR = 0:react = 100
if vital > 100 and mutagenV = 0:vital = 100
if will > 100 and mutagenW = 0:will = 100
if intel > 100 and mutagenI = 0 and student = 0 and diplom = 0:intel = 100

if stren > 150 and mutagenP = 1:stren = 150
if speed > 150 and mutagenS = 1:speed = 150
if agil > 150 and mutagenA = 1:agil = 150
if react > 150 and mutagenR = 1:react = 150
if vital > 150 and mutagenV = 1:vital = 150
if will > 150 and mutagenW = 1:will = 150
if intel > 150 and mutagenI = 1 and student = 0 and diplom = 0:intel = 150

if stren > 200 and mutagenP = 2:stren = 200
if speed > 200 and mutagenS = 2:speed = 200
if agil > 200 and mutagenA = 2:agil = 200
if react > 200 and mutagenR = 2:react = 200
if vital > 200 and mutagenV = 2:vital = 200
if will > 200 and mutagenW = 2:will = 200
if intel > 200 and mutagenI = 2 and student = 0 and diplom = 0:intel = 200

if stren > 250 and mutagenP = 3:stren = 250
if speed > 250 and mutagenS = 3:speed = 250
if agil > 250 and mutagenA = 3:agil = 250
if react > 250 and mutagenR = 3:react = 250
if vital > 250 and mutagenV = 3:vital = 250
if will > 250 and mutagenW = 3:will = 250
if intel > 250 and mutagenI = 3 and student = 0 and diplom = 0:intel = 250

if intel > 250:intel = 250

if MagikDostup = 0:
	!!!CV edition: Moved the statement after each orgasm stage to it''s own string
	$levelUpString = '<b>妳受到快樂的刺激, 魔力等級提升了</b>'

	if orgasm >= 1 and magik < 1:
		magik = 1
		glaraz += 1
		rikudo += 500
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 2 and magik < 2:
		magik = 2
		resnic += 1
		rikudo += 500
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 4 and magik < 3:
		magik = 3
		glaraz += 1
		rikudo += 500
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 8 and magik < 4:
		magik = 4
		resnic += 1
		rikudo += 500
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 16 and magik < 5:
		magik = 5
		glaraz += 1
		rikudo += 500
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 32 and magik < 6:
		magik = 6
		resnic += 1
		rikudo += 1000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 64 and magik < 7:
		magik = 7
		glaraz += 1
		rikudo += 1000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 100 and magik < 8:
		magik = 8
		rikudo += 1000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 200 and magik < 9:
		magik = 9
		rikudo += 1000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 300 and magik < 10:
		magik = 10
		rikudo += 2000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 400 and magik < 11:
		magik = 11
		rikudo += 4000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 500 and magik < 12:
		magik = 12
		rikudo += 4000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 600 and magik < 13:
		magik = 13
		rikudo += 4000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 700 and magik < 14:
		magik = 14
		rikudo += 4000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 800 and magik < 15:
		magik = 15
		rikudo += 8000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 900 and magik < 16:
		magik = 16
		rikudo += 8000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 1000 and magik < 17:
		magik = 17
		rikudo += 8000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 1100 and magik < 18:
		magik = 18
		rikudo += 8000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 1200 and magik < 19:
		magik = 19
		rikudo += 8000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 1300 and magik < 20:
		magik = 20
		rikudo += 16000
		$stat_msg += '<<$levelUpString>>'
	elseif orgasm >= 1400 and magik < 20:
		magik = 21
		rikudo += 21000
		$stat_msg += '<<$levelUpString>>'
	end
	!!!
end

if DOM > 100: DOM = 100
if SUB > 100: SUB = 100
if VAF > 200: VAF = 200

if SUB >= DOM and DOM > 0:SUB = SUB - DOM & DOM = 0
if DOM >= SUB and SUB > 0:DOM = DOM - SUB & SUB = 0

if horny < 0:horny = 0
healthmax = (vital*10 + stren*5)
willpowermax = (intel*5 + will*5)
mannamax = (intel * magik) + magik * 100 + vital * 10 + rikudo
if healthmax < 1:healthmax = 1
if willpowermax < 1:willpowermax = 1
if mannamax < 1:mannamax = 1
!pre-calc max value for later display by illume
if Enable_lockhorny > 0 and horny < Enable_lockhorny - 1:horny = Enable_lockhorny - 1
if horny < 0:horny = 0
if health > healthmax:health = healthmax
if willpower > willpowermax:willpower = willpowermax
if manna > mannamax:manna = mannamax

!!musle = strenbuf + vitalbuf

!!!!!!!!!!!
!!!TIME!!!
!!!!!!!!!!!


! WD: totminut fix ~ Calculates total minutes since NYE 2000

if nminut ! minut:
	! Zero totminut will be at New Years Eve of 2015

	! Years since 2015 in days
	totminut = (year - 2015) * 365

	! Days this year
	if month > 1 : totminut += 31 & ! Jan
	if month > 2 : totminut += 28 & ! Feb
	if month > 3 : totminut += 31 & ! Mar
	if month > 4 : totminut += 30 & ! Apr
	if month > 5 : totminut += 31 & ! May
	if month > 6 : totminut += 30 & ! Jun
	if month > 7 : totminut += 31 & ! Jul
	if month > 8 : totminut += 31 & ! Aug
	if month > 9 : totminut += 30 & ! Sep
	if month > 10: totminut += 31 & ! Oct
	if month > 11: totminut += 30 & ! Nov
	if month > 12: totminut += 31 & ! Dec
	if month > 13: totminut += 31 & ! Jan rollover sanity check
	totminut += day

	! Hours
	totminut = totminut * 24 + hour

	! Minutes
	totminut = totminut * 60 + minut + sec / 60

	nminut = minut
end

! WD: 'daystart' Should start at 1
if daystart = 0: daystart = 1

if vgape > 0:
	if vgapetime = 0: vgapetime = totminut
	if (vgapetime + 2500) <= totminut:vgape -= 1 & vgapetime = 0
end

if agape > 0:
	if agapetime = 0: agapetime = totminut
	if (agapetime + 2500) <= totminut:agape -= 1 & agapetime = 0
end

if spanked > 0:
	if spankedtime = 0: spankedtime = totminut
	if (spankedtime + 2500) <= totminut:spanked -= 1 & spankedtime = 0
end

if sec >= 60:
	:loopsec
	minut += 1
	sec -= 60

	if sec >= 60:jump 'loopsec'
end

if minut >= 60:
	:loopmin
	if SbrosNerdism = 1:nerdism = will / 10 - orgasm
	if nerdism < 0:nerdism = 0 & msg'由於長期遠離書本, 你戒除了閱讀的習慣'
	if obkvsdam > 0:obkvsdam = rand(2, 5)
	hour += 1
	if insleep = 1:
		energy -= 4
		water -= 4
		son -= 4
	else
		energy -= 8
		water -= 8
		son -= 4
	end
	minut -= 60
	gs 'femcyc'

	if alko > 0 and alkoblock = 0:
		if alko >= 4:
			if beg > 0:beg -= 1
			if volleyboll > 0:volleyboll -= 1

			if alko >= 6:
				if stren > 10:stren -= 1 & salo += 3
				if vital > 10:vital -= 1 & salo += 3
				if intel > 10:intel -= 1

				alko -= rand (1, 2)
			end
		end

		alko -= 1
	end

	!!!!!!!!!!!!!
	!!!Avtoboy!!!
	!!!!!!!!!!!!!
	if hour >= 8 and hour <= 23 and NPCnum > 10 and InSleep = 0:
		:markturnirotbort
		xf = 0
		:markturnirotbortt
		xf += 1
		turnirFighter[xf] = rand(2, NPCnum)

		if xf < 2:jump 'markturnirotbortt'
		if turnirFighter[1] = turnirFighter[2]:jump 'markturnirotbort'

		a = turnirFighter[1]
		i = turnirFighter[2]
		NoView = 1
		gs 'Autofight'
	end
	!!!!!!!!!!!!!

	if shame > 0:shame -= 1 & manna -= 5
	if smoker >= 20:smokerNeed += 1
	if smokerNeed > 10 and InSleep = 0:manna -= manna * 10/100
	if smokerNeed > 300:smokerNeed = 0 & smoker = 0
	if StrongNarkota > 0:StrongNarkota -= 1
	if SLomka > 0:SLomka += 1 & manna -= 500
	if SLomka > 100 and StrongNarkota <= 0:SLomka = 0 & SNarkTimes = 0 &msg'妳成功的戒除了毒癮(這只是個遊戲!現實中沒這麼簡單)'

	if narkoman = 1 and narkday ! daystart:
		health -= 5
		willpower -= 5
		manna -= 10
	end

	if tanga = 0 and PCloPants > 0 and $clothingworntype ! 'nude':mosolpred += 1
	

	if mosolpred >= 10:
		mosol += 1
		mosolpred -= 10
	end

	if mosol >= 50:
		manna -= 5
	end

	if energy > 100:
		fat += (energy - 100) / 2
		energy = 100
	end

	if mosol > 0:
		if tanga = 1 or $clothingworntype = 'nude':mosol -= 1
	end
	
	if lipbalmstat > 0:lipbalmstat -= 1
	if sickstage = 1:sick += 1 & sicktimer += 1
	if sickstage = 1 and sicktimer >= 80:sicktimer = 0 & sickstage = 2
	if sickstage = 2 and sick > 0:sick -= 1
	if sickstage = 2 and sick <= 0:sick = 0 & sickstage = 0
	if perkice > 0:perkice -= 1
	if frost > 0:frost -= 1

	if KGOLpers > 0:
		if KGHP < KGHPMAX:KGHP += 10
		if KGManna < KGMannaMax:KGManna += 10
	end

	if minut >= 60:jump 'loopmin'
end

if hour >= 24: gs 'cikl'

if minut < 10: 
	$nilmin = '0' 
else 
	$nilmin = '' 
end

if kid > 0:
	i = arrsize('yearkid')
	:LoopKidAgeCounter
	if 	i >= 0:
		if yearkid[i] = year + 1 and monthkid[i] = month and daykid[i] = day: msg'今天是 <<$kidname[i]>> 的生日.'
		i -= 1
		jump 'LoopKidAgeCounter'
	end
end
!!moodlet
!!mannamax
!!totminut

if Mud >= totminut:
	manna = mannamax
	$mud = '妳興致高昂'
else
	$mud = ''
end

if nerdism > 0:
	if lastreadday = daystart:
		!!<font color="green">You are completely healthy shine and a healthy glow.</font>
		manna = mannamax
		SbrosNerdism = 0
		$mudnerd = '<font color = green>妳沉浸在書本的幻想世界裡</font>'
	end

	if lastreadday + 1 = daystart:
		!!manna = mannamax
		SbrosNerdism = 0
		$mudnerd = '<font color = black>妳覺得有點無聊, 想要埋首進入書鄉</font>'
	end
	if lastreadday + 2 = daystart:
		SbrosNerdism = 0

		if manna > mannamax / 2:manna -= 10

		$mudnerd = '<font color = red>妳真的想要讀點東西</font>'
	end

	if lastreadday + 3 <= daystart:
		if manna > mannamax / 10:manna -= 10

		SbrosNerdism = 1
		$mudnerd = '<font color = red><b>妳渴望讀點東西, 什麼都好！</b></font>'
	end

	!!$mudnerd += 'lastread<<lastread>> totminut<<totminut>> <<totminut - lastread>>'
else
	SbrosNerdism = 0
	$mudnerd = ''
end

if bimbolevel > 0:
	if bimbowithdrawal = 0:
		$bimbostatus = '<font color = #FF1493>妳覺得, 嗯, 性真是美妙.</font>'
	elseif bimbowithdrawal = 1:
		$bimbostatus = '<font color="green">妳覺得, 嗯, 今天好空虛.</font>'
	elseif bimbowithdrawal < 4:
		$bimbostatus = '<font color="black">妳覺得, 嗯, 今天好空虛.</font>'
	elseif bimbowithdrawal < 6:
		if manna > mannamax / 2:manna -= 10
		$bimbostatus = '<font color="red">妳覺得, 嗯, 心情很不好.</font>'
	elseif bimbowithdrawal >= 6:
		if manna > mannamax / 10:manna -= 10
		$bimbostatus= '<font color="red"><b>妳覺得, 嗯, 真是糟透了!</b></font>'
	end

	if bimbowithdrawal > 30:
		bimbowithdrawal = 0
		bimbo = 0
		bimbolevel = 0
		$stat_msg += '<BR>妳突然清醒過來, 腦子裡不再充滿桃色的想法.' & MSG'妳突然清醒過來, 腦子裡不再充滿桃色的想法.'
	end
else
	bimbowithdrawal = 0
	$bimbostatus = ''
end

!
! WD: Triggers every 15 minutes if not sleeping
! Update to remove long iterations on loading saves from previous versions

! Do Nothing if sleeping, invalid Trig15Minute value or more than 6 hours 16 minute pass
if InSleep ! 0 or Trig15Minute > totminut or (totminut - Trig15Minute) >= 376:
	set Trig15Minute = totminut

! More than 15 minutes since last check
else
	if (totminut - Trig15Minute) >= 15:

		! begin Loop
		:Trig15MinuteLoop

		! Horny Actions
		if horny > 100:
			set Trig15Minute = totminut
		else
			set Trig15Minute += 15

			! WD: Fix vibrator arousal to trigger every 15 minutes, moved from 'time' location
			if vibratorin = 1:horny += rand(1, 5)

			! WD: Fixed Piecing arousal to trigger every 15 minutes, instead of once per day
			if horny < 70:
				hornystat = 0
				if pirsF = 1:hornystat += 1
				if pirsG = 1:hornystat += 1
				if pirsGL = 1:hornystat += 1
				hornyStat = hornystat * 4					
				if pirsA = 1:hornystat += 1
				if pirsB = 1:hornystat += 1
				if pirsC = 1:hornystat += 1
				if pirsD = 1:hornystat += 1
				if pirsE = 1:hornystat += 1
				if pirsN = 1:hornystat += 1
				set horny += hornyStat / 4 + rand(1,4) <= hornyStat mod 4 & ! Apply 1/4 of value on average
			end

			! WD: Clothing
			if CloStyle = 4 and horny < 70:set horny += rand(0, 2)
			if (PCloSkirt > 5 or PCloPants > 5) and horny < 50:set horny += rand(0, 1)
			if PCloTopCut = 4 and horny < 50:set horny += rand(0, 1)
		end
			
!!SECTION: Bimbo (15 minute interval update checks)
		if bimbolevel = 2:
			if manna < mannamax:manna += ((manna / 100) * (rand(0,1)))
			if arousal < 25:arousal = 25
			if arousal < 100:arousal += 1
		end
		if bimbolevel = 3:
			if (manna < mannamax):manna += ((manna / 100) * (rand(0,2)))
			if arousal < 50:arousal = 50
			if arousal < 100:arousal += 2
		end
!! level four, adds supernatural-style succubus abilities. May be removed or spun off into its own TF or obtained through special means unlike the rest.
!! Also planning to add much more severe and rapidly advancing penalties for not having sex to offset bonuses
		if bimbolevel = 4:
			if arousal < 80:arousal = 80
			if arousal < 100:arousal += 2
			if cumlip > 0:sexnutrition += cumlip & cumlip = 0 & absorbflag = 1
			if swallow > 0:sexnutrition += swallow * 2 & swallow = 0 & absorbflag = 1
			if cumpussy > 0:sexnutrition += cumpussy * 4 & cumpussy = 0 & absorbflag = 1
			if cumanus > 0:sexnutrition += cumanus * 4 & cumanus = 0 & absorbflag = 1
			if absorbflag = 1:
				if (manna < mannamax):manna += ((manna / 100) * 5)
				$stat_msg += '<BR>妳吸收了身上的精液, 滋養妳的身體.'
				absorbflag = 0
			end
			if sexnutrition > 0:
				water += sexnutrition / 2
				energy += sexnutrition / 3
				son += sexnutrition / 4
				sexnutrition = 0
			end
		end
!! safeguards to prevent bimbolevel from going above or below its intended limits
		if bimbolevel > 4:bimbolevel = 4
		if bimbolevel < 0:bimbolevel = 0			

		! do loop
		if (totminut - Trig15Minute) >= 15:jump 'Trig15MinuteLoop'
	end
end

!!!!!!!!!!!!!
!!!ENERGY!!!
!!!!!!!!!!!!!

if son < 0:
	son = 0
	manna -= 5
end

if energy < 0:
	energy = 0
	health -= 5

	if fat = 0 and stren + vital > 0:
		stren -= 1
		vital -= 1
		energy += 8
	elseif fat = 0 and stren + vital <= 0:
		if Enable_nogameover = 0 :
			over = 3
			gt'gameover'
			exit
		else
			$stat_msg += '<BR><font color=red><B>你已經餓死了, 但是作弊模式吊著你的命</B></font>'
			fat = 1
		end
	elseif fat > 0:
		fat -= 1
		energy += 8
	end
end

if water < 0:
	water = 1
	health -= 10
	willpower -= 1
end

if manna <= 0:
	manna = 1
	health -= 5
	willpower -= 5
	!!fat += 1
end

if health < 0:
	if Enable_nogameover = 0 :
		over = 1
		gt'gameover'
		exit
	else
		$stat_msg += '<BR><font color=red><B>你的身體已經死了, 但是作弊模式吊著你的命</B></font>'
		health = 0
	end
end

if willpower <= 0 :
	!this will set willpower to 0 correctly; then check mana =1 for insane-gameover(willpower=0&mana=1).
	if manna <= 1:
		if Enable_nogameover = 0 :
			if StoryLine = 0:
				over = 2
			else
				over = 10
			end
			gt'gameover'
			exit
		else
			$stat_msg += '<BR><font color=red><B>你已經精神崩潰了, 但是作弊模式吊著你的命</B></font>'
			manna = 1
		end
	end
	willpower = 0
end

if saloonce = 0 and opPRE = 0:
	saloonce = 1
	salo = fat
	strenbuf = stren
	vitalbuf = vital
	agilbuf = agil
end

!cars-increase/depressed mood after orgasm/rape
if rape > 0 and rape ! rape_count and orgasm = orgasm_count:
	manna = 10
	rape_count = rape
elseif orgasm > 0 and orgasm ! orgasm_count:
	manna = mannamax
	orgasm_count = orgasm
elseif  rape > 0 and rape ! rape_count and orgasm > 0 and orgasm ! orgasm_count:
	manna = mannamax
	orgasm_count = orgasm
	rape_count = rape
end

gs 'body'
!!!!!!!!!!!!!
if health >= healthmax:
	$health = '<font color = green>妳像陽光般健康閃亮</font>'
elseif health >= healthmax * 75 / 100:
	$health = '<font color = blue>妳看起來健康, 但是身體有點隱患</font>'
elseif health >= healthmax * 50 / 100:
	$health = '<font color = brown>妳覺得好像生病了</font>'
elseif health >= healthmax * 25 / 100:
	$health = '<font color = red>妳感到很不舒服</font>'
else
	$health = '<font color = red>妳接近死亡邊緣了'
end

if willpower >= willpowermax:
	$willpower = '<font color = green>妳的心智迅捷有力</font>'
elseif willpower >= willpowermax * 75 / 100:
	$willpower = '<font color = blue>妳的頭腦還算清醒</font>'
elseif willpower >= willpowermax * 50 / 100:
	$willpower = '<font color = brown>妳感到有點壓抑</font>'
elseif willpower >= willpowermax * 25 / 100:
	$willpower = '<font color = red>妳覺得很沮喪</font>'
else
	$willpower = '<font color = red>妳已經在瘋狂的邊緣了</font>'
end

if manna >= mannamax:
	$manna = '<font color = green>妳的心情很好</font>'
elseif manna >= mannamax * 75 / 100:
	$manna = '<font color = blue>妳的心情一般</font>'
elseif manna >= mannamax * 50 / 100:
	$manna = '<font color = brown>妳的心情不太好</font>'
elseif manna >= mannamax * 25 / 100:
	$manna = '<font color = red>妳的心情非常差</font>'
else
	$manna = '<font color = red>妳覺得身在地獄</font>'
end

if energy >= 80:
	$energy = '<font color = green>妳吃撐了</font>'
elseif energy >= 40:
	$energy = '<font color = blue>妳吃飽了</font>'
elseif energy >= 8:
	$energy = '<font color = brown>妳有點餓</font>'
else
	$energy = '<font color = red>妳非常餓</font>'
end

if water >= 80:
	$water = '<font color = green>妳喝撐了</font>'
elseif water >= 40:
	$water = '<font color = blue>妳還不渴</font>'
elseif water >= 8:
	$water = '<font color = brown>妳想喝水</font>'
else
	$water = '<font color = red>妳快渴死了</font>'
end

if son >= 80:
	$son = '<font color = green>妳的精力充沛</font>'
elseif son >= 40:
	$son = '<font color = blue>妳還不累</font>'
elseif son >= 8:
	$son = '<font color = brown>妳很想睡覺</font>'
else
	$son = '<font color = red>妳昏昏欲睡</font>'
end

$smstat = ''
if SUB >= 75:
	$smstat='目前狀態：完全的奴隸.'
elseif SUB >=50:
	$smstat='目前狀態：奴隸.'
elseif SUB >=25:
	$smstat='目前狀態：順從的.'
elseif SUB >0:
	$smstat='目前狀態：容易屈服.'
end
if DOM >= 75:
	$smstat='目前狀態：嚴厲的女主人.'
elseif DOM >= 50:
	$smstat='目前狀態：女主人.'
elseif DOM >= 25:
	$smstat='目前狀態：強勢的女人.'
elseif DOM > 0:
	$smstat='目前狀態：傾向主導地位.'
end

!$din_shame = {
	!!dynamic din_shame
	!!shame += 1
	!!if shame >= 5:shame = 4 & shameless += 1
	!!if shame >= 4:$shame = '<font color="red"><b>of your cheeks blaze zalivshego them blush with shame.</b></font>'
	!!if shame = 3:$shame = '<font color="red">Your cheeks are red from the blush of shame.</font>'
	!!if shame = 2:$shame = '<font color="brown">You nemnog opokrasneli shame.</font>'
	!!if shame = 1:$shame = '<font color="green">You''re a little confused.</font>'
	!!if shame <= 0:$shame = '<font color="green">You''re a little confused.</font>'
	!!gs 'stat'
}

!!if shame >= 5:shame = 4 & shameless += 1
!!if shame >= 4:$shame = '<font color="red"><b>of your cheeks blaze zalivshego them blush with shame.</b></font>'
!!if shame = 3:$shame = '<font color="red">Your cheeks are red from the blush of shame.</font>'
!!if shame = 2:$shame = '<font color="brown">You nemnog opokrasneli shame.</font>'
!!if shame = 1:$shame = '<font color="green">You''re a little confused.</font>'
!!if shame <= 0:$shame = '<font color="green">You''re a little confused.</font>'

if hour >= 8 and hour < 16 and week < 6 and StoryLine > 0 and SchoolAtestat = 0 and kanikuli = 0 and schoolProgulDay ! daystart:
	!!absence from school
	schoolprogul += 1
	schoolProgulDay = daystart
end

if workFabrika = 1 and week => 1 and week <= 5 and hour > 8 and age => 16 and age < 18 and FabrikaProgulDay ! day or workFabrika = 1 and week => 1 and week <= 5 and hour > 16 and age > 18 and FabrikaProgulDay ! day:
	!absence from work in a factory in Pavlovsky
	FabrikaProgul += 1
	FabrikaProgulDay = day
end

if hotelroom > 0:
	if hotelroom1day = daystart: $stat_msg += '<BR>妳的旅館房間租期還剩一天.'
	if hotelroom1day < daystart and hour <= 11: $stat_msg += '<BR>旅館房間今天到期, 11 點後無法再回房間'
	if hotelroom1day < daystart and hour > 11: hotelroom = 0
end

if hour >= 8 and hour < 16 and week < 6 and StoryLine > 0 and SchoolAtestat = 0 and kanikuli = 0 and schoolProgulDay ! daystart:
	!!absence from school
	schoolprogul += 1
	schoolProgulDay = daystart
end

if insleep = 0:gs 'stat_display'

CatName = 0

--- stat ---------------------------------

