# clothing
!translation to Chinese, by illume

$clothing_list_line = {
	!! a single line in a clothing list (wardrobe, etc)
	!! ARGS 0 - action type (wardrobe, sell, resize, store, forsale)
	!! ARGS 1 - clothing type
	!! ARGS 2 - clothing index

	$bgcolor = iif($bgcolor='#f3f4ee','#ffffff','#f3f4ee')

	$RESULT = '<TR bgcolor='+$bgcolor+'>'
	if Enable_clothdetail = 1:$RESULT += '<TD><<FUNC(''$short_description'', $ARGS[1])>> <<ARGS[2]>></TD>'
	if Enable_clothwidth > 0:
		clothing_temp = Enable_clothwidth-1
	else
		clothing_temp = 75 & !clothing widdefault to 75
	end
	$RESULT +='<TD><a href="exec:gt ''clothing'', ''view_clothing_item'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>"><img src="<<FUNC(''$clothing_image'', $ARGS[1], ARGS[2])>>" width="<<clothing_temp-1>>"></a></TD>'
	killvar 'clothing_temp'
	gs 'clothing_attributes', $ARGS[1], ARGS[2]

	if $ARGS[1] ! 'school' or ARGS[2] ! 6:
		$RESULT += '<TD>' + dyneval '$RESULT += <<$ARGS[1]>>H[<<ARGS[2]>>]' + '</TD>'

		if $ARGS[1] ! 'exercise' and $ARGS[1] ! 'coat' and CloStyle ! 5:
			$RESULT += '<TD>' + dyneval '$RESULT += <<$ARGS[1]>>B[<<ARGS[2]>>]' + '</TD>'
		else
			$RESULT += '<TD></TD>'
		end
	else
		$RESULT += '<TD colspan="2"></TD>'
	end

	if Enable_clothdetail = 0:
		if $ARGS[1] ! 'coat' and $ARGS[1] ! 'swimwear':
			gs 'clothing_descriptions'
			$RESULT += '<TD><<FUNC(''$short_description'', $ARGS[1])>> <<ARGS[2]>><br/><br/><<$description>></TD>'
		else
			$RESULT += '<TD>' + FUNC('$clothing_name', $ARGS[1], ARGS[2]) + '</TD>'
		end
	else
		if $ARGS[1] ! 'coat' and $ARGS[1] ! 'swimwear':
			gs 'clothing_descriptions'
			if CloQuality = 1:
			   $description = '低劣'
			elseif CloQuality = 2:
			   $description = '很差'
			elseif CloQuality = 3:
			   $description = '略差'
			elseif CloQuality = 4:
			   $description = '普通'
			elseif CloQuality = 5:
			   $description = '不錯'
			elseif CloQuality = 6:
			   $description = '優良'
			elseif CloQuality = 7:
			   $description = '傑出'
			end
			$RESULT += '<TD>(<<CloQuality>>)<BR><<$description>></TD>'
			if CloThinness = 1:
			   $description = '完全鬆垮'
			elseif CloThinness = 2:
			   $description = '有些鬆垮'
			elseif CloThinness = 3:
			   $description = '稍微合身'
			elseif CloThinness = 4:
			   $description = '合身'
			elseif CloThinness = 5:
			   $description = '稍微緊身'
			elseif CloThinness = 6:
			   $description = '緊身衣'
			end
			$RESULT += '<TD>(<<CloThinness>>)<BR><<$description>></TD>'
			if CloTopCut > 0:
			!tops
				if CloTopCut = 1:
				   $description = '完全包覆'
				elseif CloTopCut = 2:
				   $description = '保守'
				elseif CloTopCut = 3:
				   $description = '有些暴露'
				elseif CloTopCut = 4:
				   $description = '非常暴露'
				end
				$RESULT += '<TD>(<<CloTopCut>>)<BR><<$description>></TD>'
			else
				$RESULT += '<TD></TD>'
			end
			if CloPantsShortness > 0:
			   if CloPantsShortness = 1:
				  $description = '一般長褲'
			   elseif CloPantsShortness = 2:
				  $description = '七分褲'
			   elseif CloPantsShortness = 3:
				  $description = '五分褲'
			   elseif CloPantsShortness = 4:
				  $description = '三分褲'
			   elseif CloPantsShortness = 5:
				  $description = '大腿根部'
			   elseif CloPantsShortness = 6:
				  $description = '露出內褲'
			   end
				$RESULT += '<TD>(<<CloPantsShortness>>)<BR><<$description>></TD>'
			else
				$RESULT += '<TD></TD>'
			end

			if CloSkirtShortness > 0:
			   if CloSkirtShortness = 1:
				  $description = '拖地'
			   elseif CloSkirtShortness = 2:
				  $description = '及膝'
			   elseif CloSkirtShortness = 3:
				  $description = '普通'
			   elseif CloSkirtShortness = 4:
				  $description = '較短'
			   elseif CloSkirtShortness = 5:
				  $description = '迷你裙'
			   elseif CloSkirtShortness = 6:
				  $description = '迷你裙 (露陰)'
			   end
			   $RESULT += '<TD>(<<CloSkirtShortness>>)<BR><<$description>></TD>'
			else
				$RESULT += '<TD></TD>'
			end
		else
			$RESULT += '<TD colspan="5"></TD>'
		end	
	end

	if CloBimbo = 1:
		$RESULT += '<TD> ○ </TD>'
	else
		$RESULT += '<TD></TD>'
	end

	if $ARGS[1] = 'naughty' and CloQuality < 4:
		$RESULT += '<TD> ○ </TD>'
	else
		$RESULT += '<TD></TD>'
	end

	$RESULT += '<TD><a href="exec:gt ''clothing'', ''view_clothing_item'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">檢視</a>'

	if $ARGS[0] ! 'sell' and $ARGS[0] ! 'resize':
		if StoryLine = 1 and SchoolAtestat = 0 and SchoolBlock = 0 and $ARGS[1] = 'school' and ARGS[2] = 6:
			$RESULT += ' <a href="exec:gs ''clothing'', ''wear'', ''<<$ARGS[1]>>'', <<ARGS[2]>> & gt ''clothing'', ''view_clothing_list'', ''wardrobe''">穿上</a></TD>'
		elseif ($ARGS[1] ! 'coat' and $ARGS[1] ! 'swimwear') and dyneval('RESULT = <<$ARGS[1]>>H[<<ARGS[2]>>]') > 0:
			if $ARGS[1] ! 'exercise' and CloStyle ! 5 and (dyneval('RESULT = <<$ARGS[1]>>B[<<ARGS[2]>>]') < (bedra - 8) or dyneval('RESULT = <<$ARGS[1]>>B[<<ARGS[2]>>]') > (bedra + 8)):
				$RESULT += '</TD>'
			else
				$RESULT += ' <a href="exec:gs ''clothing'', ''wear'', ''<<$ARGS[1]>>'', <<ARGS[2]>> & gt ''clothing'', ''view_clothing_list'', ''wardrobe''">穿上</a></TD>'
			end
		else
			$RESULT += '</TD>'
		end

		if $ARGS[1] ! 'school' or ARGS[2] ! 6:
			$RESULT += '<TD>'
			if cloc ! 1:$RESULT += ' <a href="exec:gt ''clothing'', ''dest1'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">衣箱</a><BR>'
			if cloc ! 0:$RESULT += ' <a href="exec:gt ''clothing'', ''dest0'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">衣櫃</a><BR>'
			if cloc ! 2:$RESULT += ' <a href="exec:gt ''clothing'', ''dest2'', ''<<$ARGS[0]>>'', ''<<$ARGS[1]>>'', <<ARGS[2]>>">待售</a>'
			$RESULT += '</TD>'
		end
	end
}

if $ARGS[0] = 'dest0':
	*clr
	dynamic '<<$ARGS[2]>>S[<<ARGS[3]>>] = 0'
	gt 'clothing', 'view_clothing_list', $ARGS[1]
end

if $ARGS[0] = 'dest1':
	*clr
	dynamic '<<$ARGS[2]>>S[<<ARGS[3]>>] = 1'
	gt 'clothing', 'view_clothing_list', $ARGS[1]
end

if $ARGS[0] = 'dest2':
	*clr
	dynamic '<<$ARGS[2]>>S[<<ARGS[3]>>] = 2'
	gt 'clothing', 'view_clothing_list', $ARGS[1]
end


if $ARGS[0] = 'view_clothing_list':
	!! ARGS 0 - view_clothing_list
	!! ARGS 1 - action type (wardrobe, sell, resize, store, unwanted)
	!! ARGS 2 - brothel flag (brothel)

	act 'Leave':gt $loc, $metka

	!if $ARGS[1] ! 'sell' :'<center><table border=1><TH><a href="exec:gt ''clothing'', ''view_clothing_list'', ''wardrobe''"> 衣櫃 </a></TH><TH><a href="exec:gt ''clothing'', ''view_clothing_list'', ''store''"> 衣箱 </a></TH><TH><a href="exec:gt ''clothing'', ''view_clothing_list'', ''unwanted''"> 待售 </a></TH><TH><a href="exec:gt ''clothing_QV'', ''list''"> 看圖選穿 </a></TH></center>'
	if $ARGS[1] ! 'sell' and $ARGS[1] ! 'resize':
		'<center><table border=1><TH><a href="exec:gt ''clothing'', ''view_clothing_list'', ''wardrobe''"> 衣櫃 </a></TH><TH><a href="exec:gt ''clothing'', ''view_clothing_list'', ''store''"> 衣箱 </a></TH><TH><a href="exec:gt ''clothing'', ''view_clothing_list'', ''unwanted''"> 待售 </a></TH><TH><a href="exec:gt ''clothing_QV'', ''list''"> 看圖選穿 </a></TH></center>'
	else
		'<center><table border=1><TH><a href="exec:cloc = 0 & gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>''"> 衣櫃 </a></TH><TH><a href="exec:cloc = 1 & gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>''"> 衣箱 </a></TH><TH><a href="exec:cloc = 2 & gt ''clothing'', ''view_clothing_list'', ''<<$ARGS[1]>>''"> 待售 </a></TH></center>'
	end
	$bgcolor='#f3f4ee'
	if Enable_clothdetail = 1:
		'<table border=0 cellspacing=0 cellpadding=5><TR><TH>名稱</TH><TH>縮圖</TH><TH>耐久</TH><TH>臀圍</TH><TH>品質</TH><TH>合身</TH><TH>上身</TH><TH>褲子</TH><TH>裙子</TH><TH>金髮<BR>尤物</TH><TH>娼妓</TH><TH>動作</TH>'
	else
		'<table border=0 cellspacing=0 cellpadding=5><TR><TH>縮圖</TH><TH>耐久</TH><TH>臀圍</TH><TH>敘述</TH><TH>金髮<BR>尤物</TH><TH>娼妓</TH><TH>動作</TH>'
	end	
	if $ARGS[1] ! 'sell' : *P '<TH>移至</TH>'
	*P '</TR>'
	
	if $ARGS[1] = 'wardrobe':
		cloc = 0
		if $clothingworntype ! 'nude':act 'Strip':gs 'clothing', 'strip' & gt $loc, $metka
		if StoryLine = 1 and SchoolAtestat = 0 and SchoolBlock = 0 and ($clothingworntype ! 'school' or clothingwornnumber ! 6):*P dyneval($clothing_list_line, $ARGS[1], 'school', 6)
	end
	
	if $ARGS[1] = 'store':
		cloc = 1
	end
	
	if $ARGS[1] = 'unwanted':
		cloc = 2
	end

	i = 1
	:loopcheap
		if cheap[i] = 1 and cheapS[i] = cloc and ($clothingworntype ! 'cheap' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'cheap', i)
		i += 1
	if i <= 85:jump 'loopcheap'

	i = 1
	:loopaverage
		if average[i] = 1 and averageS[i] = cloc and ($clothingworntype ! 'average' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'average', i)
		i += 1
	if i <= 125:jump 'loopaverage'

	i = 1
	:loopexpensive
		if expensive[i] = 1 and expensiveS[i] = cloc and ($clothingworntype ! 'expensive' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'expensive', i)
		i += 1
	if i <= 147:jump 'loopexpensive'

	i = 1
	:loopformal
		if formal[i] = 1 and formalS[i] = cloc and ($clothingworntype ! 'formal' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'formal', i)
		i += 1
	if i <= 14:jump 'loopformal'

	i = 1
	:loopoffice
		if office[i] = 1 and officeS[i] = cloc and ($clothingworntype ! 'office' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'office', i)
		i += 1
	if i <= 30:jump 'loopoffice'

	i = 1
	:loopschool
		if school[i] = 1 and schoolS[i] = cloc and ($clothingworntype ! 'school' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'school', i)
		i += 1
		if i = 6: i = 7
	if i <= 14:jump 'loopschool'

	i = 1
	:loopexercise
		if exercise[i] = 1 and exerciseS[i] = cloc and ($clothingworntype ! 'exercise' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'exercise', i)
		i += 1
	if i <= 26:jump 'loopexercise'

	i = 1
	:loopnaughty
		if naughty[i] = 1 and naughtyS[i] = cloc and ($clothingworntype ! 'naughty' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'naughty', i)
		i += 1
	if i <= 67:jump 'loopnaughty'

	i = 1
	:loopexhibit
		if exhibit[i] = 1 and exhibitS[i] = cloc and ($clothingworntype ! 'exhibit' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'exhibit', i)
		i += 1
	if i <= 30:jump 'loopexhibit'

	i = 1
	:loopuniform
		if uniform[i] = 1 and uniformS[i] = cloc and ($clothingworntype ! 'uniform' or clothingwornnumber ! i):*P dyneval($clothing_list_line, $ARGS[1], 'uniform', i)
		i += 1
	if i <= 24:jump 'loopuniform'

	i = 1
	:loopswimwear
		if swimwear[i] = 1 and swimwearS[i] = cloc :*P dyneval($clothing_list_line, $ARGS[1], 'swimwear', i)
		i += 1
	if i <= 30:jump 'loopswimwear'

	'</table>' 
end

if $ARGS[0] = 'view_clothing_item':
	!! ARGS 0 - view_clothing_item
	!! ARGS 1 = action type (wardrobe, shop, sell, resize)
	!! ARGS 2 - clothing type
	!! ARGS 3 - clothing index
	!! ARGS 4 - price for shop

	cla
	'<center><img src="<<FUNC(''$clothing_image'',  $ARGS[2], ARGS[3])>>"></center>'
	$RESULT = FUNC('$short_description', $ARGS[2])
	$RESULT += '<<ARGS[3]>>'
	'<<$RESULT>>'
	gs 'clothing_attributes', $ARGS[2], ARGS[3]
	if $ARGS[2] ! 'coat' and $ARGS[2] ! 'swimwear':
		gs 'clothing_descriptions'
		'<<$description>>'
	else
		FUNC('$clothing_name', $ARGS[2], ARGS[3])
	end

	if CloBimbo = 1:'This item is considered bimbo clothing.'

	if $ARGS[2] = 'naughty' and CloQuality < 4:'This outfit can be used for prostitution.'
	if $ARGS[2] = 'uniform':
		if CloStyle2 = 1:
			'This outfit is considered to be a maid uniform.'
		else
			'This outfit is a server uniform, suitable for jobs that require one.'
		end
	end

	if $ARGS[1] ! 'shop' and ($ARGS[2] ! 'school' or $ARGS[3] ! 6):
		$RESULT = '(strength '
		dynamic '$RESULT += <<$ARGS[2]>>H[<<ARGS[3]>>]'
		if $ARGS[2] ! 'exercise' and $ARGS[2] ! 'coat' and CloStyle ! 5:
			$RESULT += ', hip size '
			dynamic '$RESULT += <<$ARGS[2]>>B[<<ARGS[3]>>]'
		end
		$RESULT += ')'
		'<<$RESULT>>'

	end

	if $ARGS[1] = 'shop':
		if dyneval('RESULT = <<$ARGS[2]>>[<<ARGS[3]>>]') = 1:
			'You already own this item.'
			act 'Leave': gt $loc, $metka
		else
			if $ARGS[2] = 'coat' and ARGS[3] = 5:
				price = ARGS[4]
			else
				price = ('<<ARGS[4]>>' * ((5 * CloQuality) + 100) / 100) * 1000 / (1250 - Clothingstock[ARGS[3]])
			end
			'Price: <<price>>'
			act 'Leave': gt $loc, $metka
			if money >= Price:
				act 'Buy':
					h = 0
					if CloStyle = 2:h = 10
					if CloStyle = 5:h = 20
					h += (3 * CloQuality)
					money -= price
					dynamic $ARGS[2] + '[<<ARGS[3]>>] = 1'
					if $ARGS[2] = 'cheap' or $ARGS[2] = 'office':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 70 + <<h>>'
					elseif $ARGS[2] = 'average':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 90 + <<h>>'
					elseif $ARGS[2] = 'formal':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 15 + <<h>>'
					elseif $ARGS[2] = 'naughty' or $ARGS[2] = 'exhibit':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 35 + <<h>>'
					elseif $ARGS[2] = 'school' or $ARGS[2] = 'uniform':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 120 + <<h>>'
					elseif $ARGS[2] = 'exercise':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 100 + <<h>>'
					elseif $ARGS[2] = 'coat':
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 250 + <<h>>'
					else
						dynamic $ARGS[2] + 'H[<<ARGS[3]>>] = 60 + <<h>>'
					end
					dynamic $ARGS[2] + 'B[<<ARGS[3]>>] = bedra'
					gt $loc, $metka
				end
			else
				'You cannot afford this outfit.'
			end
		end
	elseif $ARGS[1] = 'wardrobe':
		act 'Leave in closet':gt 'clothing', 'view_clothing_list', $ARGS[1]

		if $ARGS[2] ! 'school' or ARGS[3] ! 6:
			!! if the clothing is worn out
			if dyneval('RESULT = <<$ARGS[2]>>H[<<ARGS[3]>>]') <= 0:
				'This item is worn and is not suitable for further wear.'
				if poSkill >= 200 and tkan > 0:
					cla
					'You can repair this thanks to your sewing skill.'
					act 'Repair item':
						if rand (1, 100) < 30:
							cla
							tkan -= 1
							minut += 30
							dynamic $ARGS[2] + 'H[<<ARGS[3]>>] += 30'
							'You spend 30 minutes fixing the outfit.'
							act 'Return':gt 'clothing', 'view_clothing_list', $ARGS[1]
						else
							cla
							tkan += 3
							minut += 30
							dynamic $ARGS[2] + '[<<ARGS[3]>>] = 0'
							'Despite your skill with a needle, this outfit has proved beyond repair. All that is left of it is only good for your cloth pile.'
							act 'Return':gt 'clothing', 'view_clothing_list', $ARGS[1]
						end
					end
					act 'Leave in closet': gt 'clothing', 'view_clothing_list', $ARGS[1]
				end
			exit

			end

			!! if the clothing is a type that checks for hip size
			if $ARGS[2] ! 'exercise' and $ARGS[2] ! 'coat' and CloStyle ! 5:
				!! if the hipsize is too far from your hipsize
				dynamic 'RESULT = <<$ARGS[2]>>B[<<ARGS[3]>>]'
				if RESULT < bedra - 8 or RESULT > bedra + 8:
					if RESULT < bedra:
						'This item is too small for you to wear.'
					else
						'This item is too large for you to wear.'
					end
				   
					if poSkill >= 100 and tkan > 0:
						cla
						'You can resize this thanks to your sewing skill.'
						act 'Resize item':
							cla
							tkan -= 1
							minut += 30
							dynamic $ARGS[2] + 'B[<<ARGS[3]>>] = bedra'
							'You spend 30 minutes resizing the outfit.'
							act 'Return':gt 'clothing', 'view_clothing_list', $ARGS[1]
						end

						act 'Leave in closet': gt 'clothing', 'view_clothing_list', $ARGS[1]
					end
				exit

				end
			end
		end

		if $ARGS[2] ! 'coat' and $ARGS[2] ! 'swimwear':
			act 'Wear':
				gs 'clothing', 'wear', $ARGS[2], ARGS[3]
				dynamic $ARGS[2] + 'W[<<ARGS[3]>>] = 1'
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		end
	elseif $ARGS[1] = 'store' or $ARGS[1] = 'unwanted':
		act 'Return':gt 'clothing', 'view_clothing_list', $ARGS[1]

		if $ARGS[2] ! 'coat':
			act 'Move back to wardrobe':
				dynamic $ARGS[2] + 'W[<<ARGS[3]>>] = 1'
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		end

	elseif $ARGS[1] = 'sell':
		act 'Keep item':gt 'clothing', 'view_clothing_list', $ARGS[1]

		!! value is based on the strength remaining
		dynamic 'RESULT = <<$ARGS[2]>>H[<<ARGS[3]>>]'
		if RESULT > 45:
			'You can sell this item for 500 <b>₽</b>.'
			act 'Sell':
				dynamic '<<$ARGS[2]>>[<<ARGS[3]>>] = 0'
				money += 500
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		elseif RESULT > 30:
			'You can sell this item for 300 <b>₽</b>.'
			act 'Sell':
				dynamic '<<$ARGS[2]>>[<<ARGS[3]>>] = 0'
				money += 300
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		elseif RESULT > 0:
			'You can sell this item for 100 <b>₽</b>.'
			act 'Sell':
				dynamic '<<$ARGS[2]>>[<<ARGS[3]>>] = 0'
				money += 100
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		else
			'This item is too worn to have any sale value.'
			act 'Throw it away':
				dynamic '<<$ARGS[2]>>[<<ARGS[3]>>] = 0'
				gt 'clothing', 'view_clothing_list', $ARGS[1]
			end
		end
	elseif $ARGS[1] = 'resize':
		act 'Leave item alone':gt 'clothing', 'view_clothing_list', $ARGS[1]

		if $ARGS[2] ! 'exercise' and $ARGS[2] ! 'coat' and CloStyle ! 5:
			if dyneval('RESULT = <<$ARGS[2]>>B[<<ARGS[3]>>]') ! bedra and money >= 500:
				'You can have this resized for 500 <b>₽</b>.'
				act 'Resize':
					money -= 500
					dynamic '<<$ARGS[2]>>B[<<ARGS[3]>>] = bedra'
					gt 'clothing', 'view_clothing_list', $ARGS[1]
				end
			end
		end
	end
end

if $ARGS[0] = 'strip':
	cumspclnt = 6
	gs 'cum_cleanup'
	if cloth_vid = 1:
		last_vid = 1
	else
		last_vid = 0
	end
	if $clothingworntype = 'school':schtype = clothingwornnumber
	$lastwornclothingtype = $clothingworntype
	lastwornclothingnumber = clothingwornnumber
	$clothingworntype = 'nude'
	clothingwornnumber = 0
	CloQuality = 0
	CloThinness = 0
	CloTopCut = 0
	CloPantsShortness = 0
	CloSkirtShortness = 0
	CloStyle = 0
	CloBimbo = 0
	PCloQuality = 0
	PCloThinness = 0
	PCloTopCut = 0
	PCloPants = 0
	PCloSkirt = 0
	PCloStyle = 0
	PCloBimbo = 0
	glamur = 0
	sport = 0
	ski = 0
	ofclo = 0
	sexual = 0
	odevnesh = 0
	! WD: Bimbo look
	cloth_vid = 0
	exhibiHideNewCloth = 0

end

if $ARGS[0] = 'wear':
	!! ARGS 0 - action
	!! ARGS 1 - clothing type name
	!! ARGS 2 - clothing index

	gs 'clothing', 'strip'

	$clothingworntype = $ARGS[1]
	clothingwornnumber = ARGS[2]
	
	if $ARGS[1] ! 'coat' and $ARGS[1] ! 'swimwear':
		gs 'clothing_descriptions'
	else
		FUNC('$clothing_name', $ARGS[1], ARGS[2])
	end

	gs 'clothing_attributes', $ARGS[1], ARGS[2]

	dynamic '<<$ARGS[1]>>S[<<ARGS[2]>>] = 0'

	PCloQuality = CloQuality
	PCloThinness = CloThinness
	PCloTopCut = CloTopCut
	PCloPants = CloPantsShortness
	PCloSkirt = CloSkirtShortness
	PCloStyle = CloStyle
	PCloStyle2 = CloStyle2
	PCloBimbo = CloBimbo
	if CloBimbo = 1:bimbocl = 1

	if PCloTopCut > 1:
		if tits = 2 or tits = 3:PCloTopCut += 1
		if tits = 4 or tits = 5:PCloTopCut += 2
		if tits >= 6:PCloTopCut += 3
	end

!Calculations for clothing beauty, multiplied by natural beauty
	if PCloThinness > 0:
		if PCloThinness = 1:
			PXCloThinness = 25
		elseif PCloThinness = 2:
			PXCloThinness = 50
		elseif PCloThinness = 3:
			PXCloThinness = 100
		elseif PCloThinness = 4:
			PXCloThinness = 150
		elseif PCloThinness = 5:
			PXCloThinness = 200
		elseif PCloThinness = 6:
			PXCloThinness = 300
		elseif PCloThinness >= 7:
			PXCloThinness = 400
		end
	end

	if PCloTopCut > 0:
		if PCloTopCut = 1:
			PXCloTopCut = 25
		elseif PCloTopCut = 2:
			PXCloTopCut = 50
		elseif PCloTopCut = 3:
			PXCloTopCut = 100
		elseif PCloTopCut = 4:
			PXCloTopCut = 150
		elseif PCloTopCut = 5:
			PXCloTopCut = 200
		elseif PCloTopCut = 6:
			PXCloTopCut = 300
		elseif PCloTopCut >= 7:
			PXCloTopCut = 400
		end
	end

	if PCloSkirt > 0:
		if PCloSkirt = 1:
			PXCloSkirtShortness = 25
		elseif PCloSkirt = 2:
			PXCloSkirtShortness = 50
		elseif PCloSkirt = 3:
			PXCloSkirtShortness = 100
		elseif PCloSkirt = 4:
			PXCloSkirtShortness = 150
		elseif PCloSkirt = 5:
			PXCloSkirtShortness = 200
		elseif PCloSkirt = 6:
			PXCloSkirtShortness = 300
		elseif PCloSkirt >= 7:
			PXCloSkirtShortness = 400
		end
	end

	if PCloPants > 0:
		if PCloPants = 1:
			PXCloPantsShortness = 25
		elseif PCloPants = 2:
			PXCloPantsShortness = 50
		elseif PCloPants = 3:
			PXCloPantsShortness = 100
		elseif PCloPants = 4:
			PXCloPantsShortness = 150
		elseif PCloPants = 5:
			PXCloPantsShortness = 200
		elseif PCloPants = 6:
			PXCloPantsShortness = 300
		elseif PCloPants >= 7:
			PXCloPantsShortness = 400
		end
	end
end

if $ARGS[0] = 'wear_last_worn':
	gs 'clothing', 'wear', $lastwornclothingtype, lastwornclothingnumber
end

--- clothing ---------------------------------

