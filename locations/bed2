# bed2
!translation to Chinese, by illume

if $args[0] = 'options':
	cla
	act 'Set Alarm Clock':gt 'budilnik', 'start'
	if bedPanty = 0:
		act 'You will wear panties in the morning, click to turn off':bedpanty = 1 & gt 'bed2', 'options'
	else
		act 'You will not wear panties in the morning, click to turn on':bedpanty = 0 & gt 'bed2', 'options'
	end
	if analplug = 1:
		if bedanal = 0:
			act 'If worn, you will remove your analplug at night, click to always leave it in':bedanal = 1 & gt 'bed2', 'options'
		else
			act 'If worn, you will wear your analplug through the night, click to always remove it':bedanal = 0 & gt 'bed2', 'options'
		end
	end

	if vibrator = 1 and cheatVib = 1:
		if bedvibrator = 0:
			act 'If worn, you will remove your vibrator at night, click to always leave it in':bedvibrator = 1 & gt 'bed2', 'options'
		else
			act 'If worn, you will wear your vibrator through the night, click to always remove it':bedvibrator = 0 & gt 'bed2', 'options'
		end
	end

	act 'Return':gt 'bed2'
end

if $args[0] = '':
	gs 'stat'
	cla

	if $clothingworntype ! 'nude':
		'<center><img src="images/pics/bed.jpg"></center>'
	else
		'<center><img src="images/pics/son.jpg"></center>'
	end

	if budilnikOn = 0:
		'You lie on the bed. Alarm <a href="exec:gt ''budilnik'', ''start''">set</a> for <<timer>> hours <<timerM>> minutes and currently turned <a href="exec: budilnikOn = 1 & GT ''bed2''">off</a>.'
	else
		'You lie on the bed. Alarm <a href="exec:gt ''budilnik'', ''start''">set</a> for <<timer>> hours <<timerM>> minutes and currently turned <a href="exec: budilnikOn = 0 & GT ''bed2''">on</a>.'
	end


	if mop ! 1:'<b><font color="red">妳最好先卸妝再睡覺.</font></b>'
	if son > 80:
		'妳還不想睡.'
		if (timer * 60 + timerM)-(hour * 60 + minut) > 0:
			$temp="<<((timer * 60 + timerM)-(hour * 60 + minut))/60>>:<<((timer * 60 + timerM)-(hour * 60 + minut))-((timer * 60 + timerM)-(hour * 60 + minut))/60*60>>"
		else
			$temp="<<(1440+(timer * 60 + timerM)-(hour * 60 + minut))/60>>:<<(1440+(timer * 60 + timerM)-(hour * 60 + minut))-(1440+(timer * 60 + timerM)-(hour * 60 + minut))/60*60>>"
		end
		act '在床上躺到鬧鐘設定的時間 (<<$temp>>)':

			if disable_autosave = 0:
				if StoryLine = 0:
					savegame 'autosave.sav'
				else
					savegame 'autosave_alt.sav'
				end
			end

			gs 'clothing', 'strip'

			gt 'bed2', 'dosip'
		end
	else
		act '上床睡覺':

			if disable_autosave = 0:
				if StoryLine = 0:
					savegame 'autosave.sav'
				else
					savegame 'autosave_alt.sav'
				end
			end

			gs 'clothing', 'strip'

			gt 'bed2', 'sleep'
		end
	end
	act '設定':gt 'bed2', 'options'

	act '離開床鋪':gt $locM, $metkaM
end

if $args[0] = 'sleep':
	cls
	hapri = 0
	InSleep = 1
	cloth_vid = 0

	*clr
	if analplugIN = 1 and bedanal = 0:analplugIN = 0 & '睡覺之前, 妳把肛塞拔出來收好.'
	if vibratorIN = 1 and bedvibrator = 0:vibratorIN = 0 & '睡覺之前, 妳把跳蛋拔出來收好.'
	if fat > 5 and stringimplant = 1:silicone += 1 & fat -= 5
	
	gs 'dreams', 'start'
	''
	if Enable_sleep_wait = 0:wait 450
	if mop > 1:mop = 0 & vidageday = vidageday - 1 & '妳臉上的妝花掉了. 而且敷著化妝品睡覺對皮膚不好, 老化地快.'

	:loopson
	minut += 1
	stime += 1
	health += 5

	if vibratorIn = 1:
		vtime += 1
		if vtime >= 5:
			horny +=1
			vtime = 0
		end

		if horny = 100:
			cls
			horny = 0
			orgasm += 1
			minut += 5
			gs 'stat'
			'<center><img src="images/pic/o_face.jpg"></center>'
			'強烈的震動傳來, 妳在睡夢中被高潮驚醒. 妳看了下時鐘, 現在是 <<hour>> 點 <<minut>> 分.'

			act '繼續睡':
				*clr
				cla
				gt 'bed2', 'sleep'
			end

			act '算了, 起床吧':
				$waketext = '也許穿著跳蛋睡覺不是個好主意...'
				gt 'bed2', 'wake'
			end
		end
	end

	if stime = 60:
		stime = 0
		son += 15
		gs 'stat'
	end
	
	alarm_now = (hour * 60 + minut) mod 1440
	alarm_time = timer * 60 + timerM
	alarm_set = alarm_time - rand(1,5)

	if ((dosip = 1 or budilnikOn = 1) and alarm_now = alarm_time) or (dosip = 0 and son >= 100):
		!calculate dosip time to $temp. string looks really a temp and only in [din_bad] and [placer_sex]
		if dosip = 0 and alarm_now ! alarm_time:
			if alarm_time-alarm_now > 0:
				$temp="<<(alarm_time-alarm_now)/60>>:<<(alarm_time-alarm_now)-(alarm_time-alarm_now)/60*60>>"
			else
				$temp="<<(1440+alarm_time-alarm_now)/60>>:<<(1440+alarm_time-alarm_now)-(1440+alarm_time-alarm_now)/60*60>>"
			end
		end
		
		if dosip = 1:
			manna += 10 & !I think sleep freely should increase instead of decrease your moods.
			$waketext = '妳在床上一直躺到鬧鐘設定的時間.'
		elseif budilnikOn = 1 and alarm_now = alarm_time:
			manna -= 10
			$waketext = '<center><B><h2>鬧鐘響起</h2></B></center>'
		elseif son >= 100 and budilnikOn = 1 and alarm_now >= alarm_set:
			$waketext = '妳睡到自然醒, 鬧鐘還沒響呢.'
			act '在床上窩到鬧鐘設定的時間 (<<$temp>>)':
				gt 'bed2', 'dosip'
			end
		else
			$waketext = '妳睡到自然醒.'
			act '在床上窩到鬧鐘設定的時間 (<<$temp>>)':
				gt 'bed2', 'dosip'
			end
		end
		gs 'bed2', 'wake'
	else
		jump 'loopson'
	end
end

if $args[0] = 'wake':
	'<<$waketext>>'
	dosip = 0
	gs 'stat'
	
	if daystart < 10 and StoryLine = 0:'妳以為自己還在夢中--該死的,這不是夢, 妳真的變成女人了.'
	if daystart >= 10 and daystart < 30 and StoryLine = 0:'看著自己的身體, 妳驚訝於這個女性對睡眠的需求.'

	if kanikuli = 0 and StoryLine = 1 and SchoolAtestat = 0 and SchoolBlock = 0 and week < 6 and hour >= 4 and hour < 9:
		if $locM = 'bedrPar':
			act '起床, 換上制服, 吃早餐 (0:45)':
				if schtype = 0:schtype = 6
				if schoolH[schtype] = 0:schtype = 6
				gs 'clothing', 'wear', 'school', schtype
				minut += 45
				*clr
				cla
				if bedpanty = 0 and clrbelo > 0:
					tanga = 1
				else
					tanga =0
				end
				act '<font color = green><b>Go to school</b></font>':
					minut += 1
					gt'gschool','start'
				end
				gs 'kuhrPar'
				gs 'food', 'm_meal'
				InSleep = 0
				gs 'stat'
			end
		end
		act '起床, 換上制服 (0:15)':
			if schtype = 0:schtype = 6
			if schoolH[schtype] = 0:schtype = 6
			gs 'clothing', 'wear', 'school', schtype
			minut += 15
			gs 'bed2', 'end'
		end
	else
		if $locM = 'bedrPar':
			act '起床, 穿上衣服, 並吃點東西 (0:45)':
				gs 'clothing', 'wear_last_worn'
				minut += 45
				*clr
				cla
				if bedpanty = 0 and clrbelo > 0:
					tanga = 1
				else
					tanga =0
				end
				if $clothingworntype ! 'nude':
					act '<font color = orange><b>Go to the street</b></font>':
						if hour < 4 and motherWorry = 0 and age < 18:
							'<font color=red>Are you sure? Mother will worry if you go out in midnight.</font>'
							delact '<font color = orange><b>Go to the street</b></font>'
							act '<font color=red>Yes, Go to street</font>':gt'pavResidential'
						else
							minut += 1
							gt'pavResidential'
						end
					end
				end
				gs'kuhrPar'
				gs 'food', 'm_meal'
				InSleep = 0
				gs 'stat'
			end
		end
	end

	act '起床, 穿上衣服 (0:15)':
		gs 'clothing', 'wear_last_worn'
		minut += 15
		gs 'bed2', 'end'
	end

	act '起床 (0:10)':
		minut += 10
		gs 'bed2', 'end'
	end	
end

if $args[0] = 'dosip':
	dosip=1
	gt 'bed2','sleep'
end

if $args[0] = 'end':
	InSleep = 0
	if bedpanty = 0 and clrbelo > 0:
		tanga = 1
	else
		tanga =0

	end
	gt $locM, $metkaM
end

--- bed2 ---------------------------------

